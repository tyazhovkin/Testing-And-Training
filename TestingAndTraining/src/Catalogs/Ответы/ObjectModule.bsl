#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	Если Правильный Тогда
		ПроверитьНаличиеПравильногоОтвета(Отказ);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьНаличиеПравильногоОтвета(Отказ)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Ответы.ПорядковыйНомер,
	|	Ответы.Ответ
	|ИЗ
	|	Справочник.Ответы КАК Ответы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Вопросы КАК Вопросы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Тесты КАК Тесты
	|			ПО Вопросы.Тест = Тесты.Ссылка
	|		ПО Ответы.Владелец = Вопросы.Ссылка
	|ГДЕ
	|	Ответы.Владелец = &Владелец
	|	И Ответы.Ссылка <> &Ссылка
	|	И Ответы.Правильный = ИСТИНА
	|	И Тесты.МножественныеОтветы = ЛОЖЬ
	|	И Ответы.ПометкаУдаления = ЛОЖЬ
	|	И Ответы.Недействителен = ЛОЖЬ
	|СГРУППИРОВАТЬ ПО
	|	Ответы.ПорядковыйНомер,
	|	Ответы.Ответ";

	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Для вопроса уже присутствует правильный ответ'") + " "
			+ ВыборкаДетальныеЗаписи.ПорядковыйНомер + " " + ВыборкаДетальныеЗаписи.Ответ, , , , Отказ);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли