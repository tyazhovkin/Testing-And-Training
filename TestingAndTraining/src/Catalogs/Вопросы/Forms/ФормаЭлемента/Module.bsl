#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Иллюстрация = ТекущийОбъект.Иллюстрация.Получить();
	Если Иллюстрация <> Неопределено Тогда
		АдресИллюстрации = ПоместитьВоВременноеХранилище(Иллюстрация, УникальныйИдентификатор);
		ИллюстрацияЗадана = Истина;
	КонецЕсли;
	ОбновитьСписокОтветов();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ИллюстрацияЗадана И ЭтоАдресВременногоХранилища(АдресИллюстрации) Тогда
		ТекущийОбъект.Иллюстрация = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресИллюстрации));
		ТекущийОбъект.ЕстьИллюстрация = Истина;
	Иначе
		ТекущийОбъект.Иллюстрация = Новый ХранилищеЗначения(Неопределено);
		ТекущийОбъект.ЕстьИллюстрация = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОтветЗаписан" Тогда
		ОбновитьСписокОтветов();
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИллюстрацияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ИллюстрацияНажатиеЗавершение", ЭтотОбъект);
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ФайловаяСистемаКлиент.ЗагрузитьФайл(ОписаниеОповещения, ПараметрыЗагрузки);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтветы

&НаКлиенте
Процедура ОтветыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.Ответы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьЗначение( , ТекущиеДанные.Ссылка);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОчиститьИллюстрацию(Команда)
	АдресИллюстрации = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.ПользовательБезФотографии,
		УникальныйИдентификатор);
	ИллюстрацияЗадана = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОтвет(Команда)
	Если Параметры.Ключ.Пустая() Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗаписиВопросаЗавершение", ЭтотОбъект);
		ТекстВопроса = "Данные еще не записаны.
					   |Добавление ответа возможно только после записи данных.
					   |Данные будут записаны.";
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	Иначе
		ОткрытьФормуСозданияНовогоОтвета();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьНедействительные(Команда)
	Элементы.ОтветыОтображатьНедействительные.Пометка = Не ОтобразитьНедействительные;
	ОтобразитьНедействительные = Не ОтобразитьНедействительные;
	Если ОтобразитьНедействительные Тогда
		Элементы.ОтветыОтображатьНедействительные.Картинка = БиблиотекаКартинок.ВводимыеСимволыВидны;
	Иначе
		Элементы.ОтветыОтображатьНедействительные.Картинка = БиблиотекаКартинок.ВводимыеСимволыСкрыты;
	КонецЕсли;
	ОбновитьСписокОтветов();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИллюстрацияНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоАдресВременногоХранилища(АдресИллюстрации) Тогда
		УдалитьИзВременногоХранилища(АдресИллюстрации);
	КонецЕсли;

	АдресИллюстрации = Результат.Хранение;
	Модифицированность = Истина;
	ИллюстрацияЗадана = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписиВопросаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ЭлементЗаписан = Записать();
		Если ЭлементЗаписан Тогда
			ОткрытьФормуСозданияНовогоОтвета();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСозданияНовогоОтвета()
	ПараметрыПередачи = Новый Структура;
	ПараметрыПередачи.Вставить("Владелец", Объект.Ссылка);
	ОткрытьФорму("Справочник.Ответы.Форма.ФормаЭлемента", ПараметрыПередачи, Элементы.Ответы);
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокОтветов()
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Ответы.ПорядковыйНомер КАК Номер,
	|	Ответы.Наименование,
	|	Ответы.Ответ,
	|	Ответы.Правильный,
	|	Ответы.Ссылка,
	|	Ответы.Недействителен
	|ИЗ
	|	Справочник.Ответы КАК Ответы
	|ГДЕ
	|	Ответы.Владелец = &Владелец
	|	И Ответы.ПометкаУдаления = ЛОЖЬ
	|	%1
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядковыйНомер";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%1", ?(ОтобразитьНедействительные, "", "И Ответы.Недействителен = ЛОЖЬ"));
	
	Запрос.УстановитьПараметр("ОтобразитьНедействительные", ОтобразитьНедействительные);
	Запрос.УстановитьПараметр("Владелец", Объект.Ссылка);

	РезультатЗапроса = Запрос.Выполнить();
	Ответы.Загрузить(РезультатЗапроса.Выгрузить());
КонецПроцедуры

#КонецОбласти